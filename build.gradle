import java.time.ZonedDateTime

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-RC3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'maven'
apply plugin: 'org.junit.platform.gradle.plugin'

version = "0.0.11"
group = "net.cmpsb.cacofony"

task generateMetadata {
    dependsOn processResources
    doFirst {
        new File("$buildDir/resources/main/net/cmpsb/cacofony/metadata.xml").withOutputStream
        { out ->
            final Properties p = new Properties()
            p.setProperty("net.cmpsb.cacofony.version", project.version.toString())
            p.setProperty("net.cmpsb.cacofony.build-date",
                          String.valueOf(ZonedDateTime.now().toEpochSecond()))
            p.storeToXML(out, "Generated by Gradle")
        }
    }
}

classes.dependsOn generateMetadata

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

buildDir = 'Build'

sourceSets {
    main {
        java {
            srcDirs = ['Source']
        }

        resources {
            srcDirs = ['Resources']
        }
    }

    test {
        java {
            srcDirs = ['Test']
        }

        resources {
            srcDirs = ['Resources']
        }
    }
}


repositories {
    mavenCentral()
}

dependencies {
    // Logging.
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.slf4j:jul-to-slf4j:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.1.8'
    compile 'ch.qos.logback:logback-core:1.1.8'
    compile 'org.codehaus.groovy:groovy:2.4.+'

    compile 'net.wukl:cacodi:1.1.0'

    // CSV parser.
    compile 'com.opencsv:opencsv:3.9'

    // Yaml.
    compile 'org.yaml:snakeyaml:1.18'

    // JSON.
    compile 'com.google.code.gson:gson:2.8.+'

    // Reflections.
    compile 'org.reflections:reflections:0.9.+'

    // Freemarker.
    compile 'org.freemarker:freemarker:2.3.+'

    // Content type detection.
    compile 'com.j256.simplemagic:simplemagic:1.11'

    // Test dependencies.
    // JUnit and matchers.
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0-RC3'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.0.0-RC3'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0-RC3'

    testCompile 'org.assertj:assertj-core:3.6.2'

    // Mockito.
    testCompile 'org.mockito:mockito-core:2.23.+'

    // Test helpers.
    testCompile 'com.github.stefanbirkner:system-rules:1.16.+'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:3.1.+'
}

checkstyle {
    toolVersion = '8.16'
}

junitPlatform {
    logManager 'java.util.logging.LogManager'
}

jacoco {
    toolVersion = '0.8.2'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:///srv/dl/")
            pom.artifactId = 'cacofony'
        }
    }
}
